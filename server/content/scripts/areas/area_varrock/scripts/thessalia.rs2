// source: https://www.youtube.com/watch?v=VK_A2Vf9VE8

[opnpc1,thessalia]
if (%bunny_ears_unlocked = 0 & %scythe_unlocked = 0) { // player has not unlocked bunny_ears or scythe
    @thessalia_want_to_buy;
} else if (%bunny_ears_unlocked = 1 & %scythe_unlocked = 1) { // player unlocked both
    if ((inv_total(inv, bunnyears) >= 1 | inv_total(bank, bunnyears) >= 1 | inv_total(worn, bunnyears) >= 1) &
        (inv_total(inv, scythe) >= 1 | inv_total(bank, scythe) >= 1 | inv_total(worn, scythe) >= 1)) { // player unlocked both, and has both
        @thessalia_want_to_buy;
    } else if (inv_total(inv, bunnyears) >= 1 | inv_total(bank, bunnyears) >= 1 | inv_total(worn, bunnyears) >= 1) { // player unlocked both, but lost scythe
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your scythe back?");
        @thessalia_lost_scythe;
    } else if (inv_total(inv, scythe) >= 1 | inv_total(bank, scythe) >= 1 | inv_total(worn, scythe) >= 1) { // player unlocked both, but lost bunny_ears
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your bunny ears back?");
        @thessalia_lost_bunny_ears;
    } else { // player unlocked both, but lost both
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your scythe and bunny ears back?");
        @thessalia_lost_bunny_ears_and_scythe;
    }
} else if (%bunny_ears_unlocked = 1 & %scythe_unlocked = 0) { // player unlocked bunny_ears, not scythe
    if (inv_total(inv, bunnyears) >= 1 | inv_total(bank, bunnyears) >= 1 | inv_total(worn, bunnyears) >= 1) { //player unlocked bunny_ears, and has bunny_ears
        @thessalia_want_to_buy;
    } else { // player unlocked bunny_ears, but lost bunny ears
        ~chatnpc("<p,quiz>Do you want to buy any fine clothes? Or would you like your bunny ears back?");
        @thessalia_lost_bunny_ears;
    }
} else if (%bunny_ears_unlocked = 0 & %scythe_unlocked = 1) { // player unlocked scythe, not bunny_ears
    if (inv_total(inv, scythe) >= 1 | inv_total(bank, scythe) >= 1 | inv_total(worn, scythe) >= 1) { // player unlocked scythe, and has scythe
        @thessalia_want_to_buy;
    } else { // player unlocked scythe, but lost scythe
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your scythe back?");
        @thessalia_lost_scythe;
    }
}

[label,thessalia_want_to_buy]
~chatnpc("<p,quiz>Do you want to buy any fine clothes?");
@thessalia_what_have_you_got;

[label,thessalia_what_have_you_got]
@multi2("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2);

[label,thessalia_lost_bunny_ears]
@multi3("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2,
        "Can I have my bunny ears back?", thessalia_return_bunny_ears);

[label,thessalia_lost_scythe]
@multi3("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2,
        "Can I have my scythe back?", thessalia_return_scythe);

[label,thessalia_lost_bunny_ears_and_scythe] 
@multi4("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2,
        "Can I have my scythe back?", thessalia_return_scythe,
        "Can I have my bunny ears back?", thessalia_return_bunny_ears);

[label,thessalia_return_scythe]
~chatplayer("<p,quiz>Can I have my scythe back?");
~chatnpc("<p,happy>Certainly. Here you go.");
inv_add(inv, scythe, 1);
~chatnpc("<p,quiz>Now, would you like to buy any clothes?");
@thessalia_what_have_you_got;

[label,thessalia_return_bunny_ears]
~chatplayer("<p,quiz>Can I have my bunny ears back?");
~chatnpc("<p,happy>Certainly. Here you go.");
inv_add(inv, bunnyears, 1);
~chatnpc("<p,quiz>Now, would you like to buy any clothes?");
@thessalia_what_have_you_got;

[label,thessalia_b1_s1]
~chatplayer("<p,quiz>What have you got?"); 
~chatnpc("<p,happy>Well, I have a number of fine pieces of clothing on sale or, if you prefer I can offer you an exclusive, total clothing makeover?");
@multi2("Tell me more about this makeover.", thessalia_b2_s1,
        "I'd just like to buy some clothes.", thessalia_b2_s2);

[label,thessalia_b1_s2]
~chatplayer("<p,neutral>No, thank you.");
~chatnpc("<p,happy>Well, please return if you change your mind.");

[label,thessalia_b2_s1]
~chatplayer("<p,neutral>Tell me more about this makeover.");
~chatnpc("<p,happy>Certainly!");
~chatnpc("<p,happy>Here at Thessalia's fine clothing boutique, we offer a unique service where we will totally revamp your outfit to your choosing, for... wait for it...");
~chatnpc("<p,happy>A fee of only 500 gold coins! Tired of always wearing the same old outfit, day in, day out? This is the service for you!");
~chatnpc("<p,happy>So what do you say? Interested? We can change either your top, or your legwear for only 500 gold an item!");
@multi4("I'd like to change my top please.", thessalia_b3_s1,
        "I'd like to change my legwear please.", thessalia_b3_s2,
        "I'd just like to buy some clothes.", thessalia_b2_s2,
        "No, thank you.", thessalia_b1_s2);

[label,thessalia_b3_s1]
%if1 = 0;
%if2 = 0;
%if3 = 0;

~chatnpc("<p,happy>Just select what style and colour you would like from this catalogue, and then give me the 500 coins when you've picked.");

if (gender = ^gender_male) {
    if_openmain(player_kit_tailor_torso_man);
} else {
    if_openmain(player_kit_tailor_torso_woman);
}

[label,thessalia_b3_s2]
%if1 = 0;
%if2 = 0;
%if3 = 0;

~chatnpc("<p,happy>Just select what style and colour you would like from this catalogue, and then give me the 500 coins when you've picked.");

if (gender = ^gender_male) {
    if_openmain(player_kit_tailor_legs_man);
} else {
    if_openmain(player_kit_tailor_legs_woman);
}

[label,thessalia_b2_s2]
~chatplayer("<p,neutral>I'd just like to buy some clothes.");
~openshop_activenpc;

// ----

[if_button,player_kit_tailor_torso_man:plain]
%if1 = 1;

[if_button,player_kit_tailor_torso_man:light_buttons]
%if1 = 2;

[if_button,player_kit_tailor_torso_man:jacket]
%if1 = 3;

[if_button,player_kit_tailor_torso_man:dark_buttons]
%if1 = 4;

[if_button,player_kit_tailor_torso_man:stitching]
%if1 = 5;

[if_button,player_kit_tailor_torso_man:shirt]
%if1 = 6;

[if_button,player_kit_tailor_torso_man:two_toned]
%if1 = 7;

[if_button,player_kit_tailor_torso_man:regular]
%if2 = 1;

[if_button,player_kit_tailor_torso_man:musclebound]
%if2 = 2;

[if_button,player_kit_tailor_torso_man:loose_sleeved]
%if2 = 3;

[if_button,player_kit_tailor_torso_man:large_cuffed]
%if2 = 4;

[if_button,player_kit_tailor_torso_man:thin_sleeved]
%if2 = 5;

[if_button,player_kit_tailor_torso_man:shoulder_pads]
%if2 = 6;

[if_button,player_kit_tailor_torso_man:khaki]
%if3 = 1;

[if_button,player_kit_tailor_torso_man:black]
%if3 = 2;

[if_button,player_kit_tailor_torso_man:crimson]
%if3 = 3;

[if_button,player_kit_tailor_torso_man:navy]
%if3 = 4;

[if_button,player_kit_tailor_torso_man:straw]
%if3 = 5;

[if_button,player_kit_tailor_torso_man:white]
%if3 = 6;

[if_button,player_kit_tailor_torso_man:red]
%if3 = 7;

[if_button,player_kit_tailor_torso_man:blue]
%if3 = 8;

[if_button,player_kit_tailor_torso_man:green]
%if3 = 9;

[if_button,player_kit_tailor_torso_man:yellow]
%if3 = 10;

[if_button,player_kit_tailor_torso_man:purple]
%if3 = 11;

[if_button,player_kit_tailor_torso_man:orange]
%if3 = 12;

[if_button,player_kit_tailor_torso_man:rose]
%if3 = 13;

[if_button,player_kit_tailor_torso_man:lime]
%if3 = 14;

[if_button,player_kit_tailor_torso_man:cyan]
%if3 = 15;

[if_button,player_kit_tailor_torso_man:emerald]
%if3 = 16;

[if_button,player_kit_tailor_torso_man:confirm]
if (%if1 = 0 | %if2 = 0 | %if3 = 0) {
    mes("You must make a selection from all three categories.");
    return;
}
if_close;
if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%if3 - 1);

if (%if1 = 1) {
    setidkit(man_torso_plain, $color);
} else if (%if1 = 2) {
    setidkit(man_torso_light_buttons, $color);
} else if (%if1 = 3) {
    setidkit(man_torso_jacket, $color);
} else if (%if1 = 4) {
    setidkit(man_torso_dark_buttons, $color);
} else if (%if1 = 5) {
    setidkit(man_torso_stitching, $color);
} else if (%if1 = 6) {
    setidkit(man_torso_shirt, $color);
} else if (%if1 = 7) {
    setidkit(man_torso_two_toned, $color);
}

if (%if2 = 1) {
    setidkit(man_arms_regular, $color);
} else if (%if2 = 2) {
    setidkit(man_arms_musclebound, $color);
} else if (%if2 = 3) {
    setidkit(man_arms_loose_sleeved, $color);
} else if (%if2 = 4) {
    setidkit(man_arms_large_cuffed, $color);
} else if (%if2 = 5) {
    setidkit(man_arms_thin_sleeved, $color);
} else if (%if2 = 6) {
    setidkit(man_arms_shoulder_pads, $color);
}

@thessalia_finished_makeover;

// ----

[if_button,player_kit_tailor_legs_man:plain]
%if1 = 1;

[if_button,player_kit_tailor_legs_man:shorts]
%if1 = 2;

[if_button,player_kit_tailor_legs_man:flares]
%if1 = 3;

[if_button,player_kit_tailor_legs_man:turn_ups]
%if1 = 4;

[if_button,player_kit_tailor_legs_man:khaki]
%if3 = 2;

[if_button,player_kit_tailor_legs_man:black]
%if3 = 3;

[if_button,player_kit_tailor_legs_man:crimson]
%if3 = 4;

[if_button,player_kit_tailor_legs_man:navy]
%if3 = 5;

[if_button,player_kit_tailor_legs_man:straw]
%if3 = 6;

[if_button,player_kit_tailor_legs_man:white]
%if3 = 7;

[if_button,player_kit_tailor_legs_man:red]
%if3 = 8;

[if_button,player_kit_tailor_legs_man:blue]
%if3 = 9;

[if_button,player_kit_tailor_legs_man:green]
%if3 = 10;

[if_button,player_kit_tailor_legs_man:yellow]
%if3 = 11;

[if_button,player_kit_tailor_legs_man:purple]
%if3 = 12;

[if_button,player_kit_tailor_legs_man:orange]
%if3 = 13;

[if_button,player_kit_tailor_legs_man:rose]
%if3 = 14;

[if_button,player_kit_tailor_legs_man:lime]
%if3 = 15;

[if_button,player_kit_tailor_legs_man:cyan]
%if3 = 16;

[if_button,player_kit_tailor_legs_man:emerald]
%if3 = 1;

[if_button,player_kit_tailor_legs_man:confirm]
if (%if1 = 0 | %if3 = 0) {
    mes("You must make a selection from both categories.");
    return;
}

if_close;

if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%if3 - 1);

if (%if1 = 1) {
    setidkit(man_legs_plain, $color);
} else if (%if1 = 2) {
    setidkit(man_legs_shorts, $color);
} else if (%if1 = 3) {
    setidkit(man_legs_flares, $color);
} else if (%if1 = 4) {
    setidkit(man_legs_turn_ups, $color);
}

@thessalia_finished_makeover;

[if_button,player_kit_tailor_torso_woman:plain] %if1 = 1;
[if_button,player_kit_tailor_torso_woman:crop_top] %if1 = 2;
[if_button,player_kit_tailor_torso_woman:polo_neck] %if1 = 3;
[if_button,player_kit_tailor_torso_woman:simple] %if1 = 4;
[if_button,player_kit_tailor_torso_woman:torn] %if1 = 5;

[if_button,player_kit_tailor_torso_woman:short_sleeves] %if2 = 1;
[if_button,player_kit_tailor_torso_woman:bare_arms] %if2 = 2;
[if_button,player_kit_tailor_torso_woman:muscley] %if2 = 3;
[if_button,player_kit_tailor_torso_woman:long_sleeved] %if2 = 4;
[if_button,player_kit_tailor_torso_woman:large_cuffs] %if2 = 5;

[if_button,player_kit_tailor_torso_woman:com_91] %if3 = 1;
[if_button,player_kit_tailor_torso_woman:com_92] %if3 = 2;
[if_button,player_kit_tailor_torso_woman:com_93] %if3 = 3;
[if_button,player_kit_tailor_torso_woman:com_94] %if3 = 4;
[if_button,player_kit_tailor_torso_woman:com_95] %if3 = 5;
[if_button,player_kit_tailor_torso_woman:com_96] %if3 = 6;
[if_button,player_kit_tailor_torso_woman:com_97] %if3 = 7;
[if_button,player_kit_tailor_torso_woman:com_98] %if3 = 8;
[if_button,player_kit_tailor_torso_woman:com_99] %if3 = 9;
[if_button,player_kit_tailor_torso_woman:com_100] %if3 = 10;
[if_button,player_kit_tailor_torso_woman:com_101] %if3 = 11;
[if_button,player_kit_tailor_torso_woman:com_102] %if3 = 12;
[if_button,player_kit_tailor_torso_woman:com_103] %if3 = 13;
[if_button,player_kit_tailor_torso_woman:com_104] %if3 = 14;
[if_button,player_kit_tailor_torso_woman:com_105] %if3 = 15;
[if_button,player_kit_tailor_torso_woman:com_106] %if3 = 16;

[if_button,player_kit_tailor_torso_woman:confirm]
if (%if1 = 0 | %if2 = 0 | %if3 = 0) {
    mes("You must make a selection from all three categories.");
    return;
}
if_close;
if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%if3 - 1);

if (%if1 = 1) {
    setidkit(woman_torso_plain, $color);
} else if (%if1 = 2) {
    setidkit(woman_torso_crop_top, $color);
} else if (%if1 = 3) {
    setidkit(woman_torso_polo_neck, $color);
} else if (%if1 = 4) {
    setidkit(woman_torso_simple, $color);
} else if (%if1 = 5) {
    setidkit(woman_torso_torn, $color);
}

if (%if2 = 1) {
    setidkit(woman_arms_short_sleeves, $color);
} else if (%if2 = 2) {
    setidkit(woman_arms_bare_arms, $color);
} else if (%if2 = 3) {
    setidkit(woman_arms_muscley, $color);
} else if (%if2 = 4) {
    setidkit(woman_arms_long_sleeved, $color);
} else if (%if2 = 5) {
    setidkit(woman_arms_large_cuffs, $color);
}

@thessalia_finished_makeover;

[if_button,player_kit_tailor_legs_woman:com_133] %if1 = 1;
[if_button,player_kit_tailor_legs_woman:com_134] %if1 = 2;
[if_button,player_kit_tailor_legs_woman:com_135] %if1 = 3;
[if_button,player_kit_tailor_legs_woman:com_136] %if1 = 4;
[if_button,player_kit_tailor_legs_woman:com_137] %if1 = 5;
[if_button,player_kit_tailor_legs_woman:com_138] %if1 = 6;
[if_button,player_kit_tailor_legs_woman:com_139] %if1 = 7;
[if_button,player_kit_tailor_legs_woman:com_140] %if1 = 8;

[if_button,player_kit_tailor_legs_woman:com_91] %if3 = 2;
[if_button,player_kit_tailor_legs_woman:com_92] %if3 = 3;
[if_button,player_kit_tailor_legs_woman:com_93] %if3 = 4;
[if_button,player_kit_tailor_legs_woman:com_94] %if3 = 5;
[if_button,player_kit_tailor_legs_woman:com_95] %if3 = 6;
[if_button,player_kit_tailor_legs_woman:com_96] %if3 = 7;
[if_button,player_kit_tailor_legs_woman:com_97] %if3 = 8;
[if_button,player_kit_tailor_legs_woman:com_98] %if3 = 9;
[if_button,player_kit_tailor_legs_woman:com_99] %if3 = 10;
[if_button,player_kit_tailor_legs_woman:com_100] %if3 = 11;
[if_button,player_kit_tailor_legs_woman:com_101] %if3 = 12;
[if_button,player_kit_tailor_legs_woman:com_102] %if3 = 13;
[if_button,player_kit_tailor_legs_woman:com_103] %if3 = 14;
[if_button,player_kit_tailor_legs_woman:com_104] %if3 = 15;
[if_button,player_kit_tailor_legs_woman:com_105] %if3 = 16;
[if_button,player_kit_tailor_legs_woman:com_106] %if3 = 1;

[if_button,player_kit_tailor_legs_woman:confirm]
if (%if1 = 0 | %if3 = 0) {
    mes("You must make a selection from both categories.");
    return;
}

if_close;

if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%if3 - 1);

if (%if1 = 1) {
    setidkit(woman_legs_plain, $color);
} else if (%if1 = 2) {
    setidkit(woman_legs_shorts, $color);
} else if (%if1 = 3) {
    setidkit(woman_legs_flares, $color);
} else if (%if1 = 4) {
    setidkit(woman_legs_turn_ups, $color);
} else if (%if1 = 5) {
    setidkit(woman_legs_turn_ups_2, $color);
} else if (%if1 = 6) {
    setidkit(woman_legs_turn_ups_3, $color);
} else if (%if1 = 7) {
    setidkit(woman_legs_turn_ups_4, $color);
} else if (%if1 = 8) {
    setidkit(woman_legs_turn_ups_5, $color);
}

@thessalia_finished_makeover;

[label,thessalia_finished_makeover]
buildappearance(worn);
inv_del(inv, coins, 500);
if (npc_find(coord, thessalia, 4, 0) = false) {
    return;
}
def_int $rand = random(6);
if ($rand = 0) {
    ~chatnpc("<p,happy>Totally cool! That outfit looks great!");
} else if ($rand = 1) {
    ~chatnpc("<p,happy>Very stylish! It really suits you!");
} else if ($rand = 2) {
    ~chatnpc("<p,happy>Woah! Fabulous! You look absolutely great!");
} else if ($rand = 3) {
    ~chatnpc("<p,happy>Wow! Good choice! You're looking great!");
} else if ($rand = 4) {
    ~chatnpc("<p,happy>WoooOOOooo! Hot stuff!");
} else if ($rand = 5) {
    ~chatnpc("<p,happy>Fantastic! You should be a model!");
}
~chatplayer("<p,happy>Thanks!");
