[ai_applayer2,kolhuman] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);
[ai_applayer2,kologre] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);
[ai_applayer2,kolspider] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^claws_of_guthix), 5);
[ai_applayer2,kolethereal] ~npc_cast_spell(~random_range(^saradomin_strike, ^claws_of_guthix), 5);
[ai_applayer2,koldemon] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);

[ai_opplayer2,kolhuman] ~npc_cast_spell(~random_range(^claws_of_guthix, ^saradomin_strike), 5);
[ai_opplayer2,kologre] ~npc_cast_spell(~random_range(^claws_of_guthix, ^saradomin_strike), 5);
[ai_opplayer2,kolspider] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^claws_of_guthix), 5);
[ai_opplayer2,kolethereal] ~npc_cast_spell(~random_range(^saradomin_strike, ^claws_of_guthix), 5);
[ai_opplayer2,koldemon] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);


[ai_queue1,kolhuman] ~npc_default_retaliate_ap;
[ai_queue1,kologre] ~npc_default_retaliate_ap;
[ai_queue1,kolspider] ~npc_default_retaliate_ap;
[ai_queue1,kolethereal] ~npc_default_retaliate_ap;
[ai_queue1,koldemon] ~npc_default_retaliate_ap;

// newer vids do npc_changetype the whole way (buggy with big npc's): https://youtu.be/CKDo9p08iRI?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o 
// Older vids use npc_del and npc_add the whole way?: https://youtu.be/mQmeH32PdNk?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o

[ai_queue3,kolhuman]
if (npc_findhero = ^false) {
    // in osrs, if you dont have kill credit the npc dies and you dont advance
    // with the exception of the last stage, demon
    gosub(npc_death);
    return;
}
%magearena = 2;
npc_setmode(none);
npc_walk(npc_coord);
npc_anim(human_castteleport, 30);
npc_delay(2);

def_coord $coord = npc_coord;
npc_del;
if (p_finduid(uid) = true) {
    p_delay(1); // temp
}
npc_add($coord, kologre, 1000);
npc_anim(npc_param(spawn_anim), 0);
spotanim_npc(smokepuff_large, 124, 34);
npc_setmode(applayer2);
npc_delay(1);
npc_say("This is only the beginning; you can't beat me!");


[ai_queue3,kologre]
if (npc_findhero = ^false) {
    gosub(npc_death);
    return;
}
%magearena = 3;
npc_setmode(none);
npc_walk(npc_coord);
npc_anim(giant_teleport, 30);
npc_delay(2);

def_coord $coord = npc_coord;
npc_del;
if (p_finduid(uid) = true) {
    p_delay(1); // temp
}
npc_add($coord, kolspider, 1000);
npc_anim(npc_param(spawn_anim), 0);
spotanim_npc(hugespider_teleport_reverse, 16, 35);
npc_setmode(opplayer2);
npc_delay(1);
npc_say("Foolish mortal; I am unstoppable.");


[ai_queue3,kolspider]
if (npc_findhero = ^false) {
    gosub(npc_death);
    return;
}
%magearena = 4;
npc_setmode(none);
npc_walk(npc_coord);
npc_anim(giantspider_teleport, 30);
npc_delay(2);

def_coord $coord = npc_coord;
npc_del;
if (p_finduid(uid) = true) {
    p_delay(1); // temp
}
npc_add(movecoord($coord, 1, 0, 1), kolethereal, 1000);
npc_anim(npc_param(spawn_anim), 0);
spotanim_npc(smokepuff_large, 124, 10);
npc_setmode(opplayer2);
npc_delay(1);
npc_say("Now you feel it... The dark energy.");


[ai_queue3,kolethereal]
if (npc_findhero = ^false) {
    gosub(npc_death);
    return;
}
%magearena = 5;
npc_setmode(none);
npc_walk(npc_coord);
npc_anim(human_castteleport, 30);
npc_delay(2);

def_coord $coord = npc_coord;
npc_del;
if (p_finduid(uid) = true) {
    p_delay(1); // temp
}
npc_add($coord, koldemon, 1000);
npc_anim(npc_param(spawn_anim), 0);
spotanim_npc(huge_teleport_reverse, 16, 0);
npc_setmode(opplayer2);
npc_delay(1);
npc_say("Aaaaaaaarrgghhhh! The power!");


[ai_queue3,koldemon]
if (npc_findhero = ^false) {
    // no death anim
    return;
}
queue(finish_mage_arena, 0, 0);
npc_delay(0);
%magearena = ^mage_arena_complete;
npc_delay(1);
npc_setmode(none);
npc_walk(npc_coord);
npc_anim(demon_portalend, 30);
spotanim_map(smokepuff_large, npc_coord, 124, 90);
npc_delay(2);
npc_changetype_keepall(arenamage2, ^max_32bit_int);
npc_anim(null, 0);
npc_setmode(playerface);
npc_delay(0);
npc_del;
