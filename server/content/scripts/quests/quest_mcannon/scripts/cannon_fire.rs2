[timer,cannon_rotate](int $dir)
// automatically starts spinning: https://youtu.be/TeQXQDaawO0?t=234, https://youtu.be/Vsnkdq8OOMg
// at https://youtu.be/WWyNTsMXjTE?t=26 the cannon doesnt auto rotate immediately 
// but at https://youtu.be/WWyNTsMXjTE?t=219 the cannon does

// i think this is because picking up the cannon doesnt reset the timer.
if (%ownedmcannon = null | loc_find(%ownedmcannon, dwarf_multicannon1) = false) {
    return;
}
def_coord $center = movecoord(loc_coord, 1, 0, 1);
def_coord $close_range = movecoord($center, 0, 0, 3);
def_coord $medium_range = movecoord($center, 0, 0, 7);
def_coord $long_range = movecoord($center, 0, 0, 14);
def_seq $seq = mcannon_ne_turn;
def_boolean $fired = false;
// start north, going clockwise
if ($dir = 0) {
    // automatically starts spinning: https://youtu.be/TeQXQDaawO0?t=234
    // at https://youtu.be/WWyNTsMXjTE?t=26 the cannon doesnt auto rotate immediately 
    // but at https://youtu.be/WWyNTsMXjTE?t=219 the cannon does
    if (%rockthrower < 1) {
        mes("Your cannon is out of ammo!"); // https://youtu.be/TeQXQDaawO0?t=239
        cleartimer(cannon_rotate);
        return;
    }
    $close_range = movecoord($center, 0, 0, 3);
    $medium_range = movecoord($center, 0, 0, 7);
    $long_range = movecoord($center, 0, 0, 14);
    $seq = mcannon_ne_turn;
} else if ($dir = 1) {
    $close_range = movecoord($center, 2, 0, 2);
    $medium_range = movecoord($center, 5, 0, 5);
    $long_range = movecoord($center, 12, 0, 12);
    $seq = mcannon_e_turn;
} else if ($dir = 2) {
    $close_range = movecoord($center, 3, 0, 0);
    $medium_range = movecoord($center, 7, 0, 0);
    $long_range = movecoord($center, 14, 0, 0);
    $seq = mcannon_se_turn;
} else if ($dir = 3) {
    $close_range = movecoord($center, 2, 0, -2);
    $medium_range = movecoord($center, 5, 0, -5);
    $long_range = movecoord($center, 12, 0, -12);
    $seq = mcannon_s_turn;
} else if ($dir = 4) {
    $close_range = movecoord($center, 0, 0, -3);
    $medium_range = movecoord($center, 0, 0, -7);
    $long_range = movecoord($center, 0, 0, -14);
    $seq = mcannon_sw_turn;
} else if ($dir = 5) {
    $close_range = movecoord($center, -2, 0, -2);
    $medium_range = movecoord($center, -5, 0, -5);
    $long_range = movecoord($center, -12, 0, -12);
    $seq = mcannon_w_turn;
} else if ($dir = 6) {
    $close_range = movecoord($center, -3, 0, 0);
    $medium_range = movecoord($center, -7, 0, 0);
    $long_range = movecoord($center, -14, 0, 0);
    $seq = mcannon_nw_turn;
} else if ($dir = 7) {
    $close_range = movecoord($center, -2, 0, 2);
    $medium_range = movecoord($center, -5, 0, 5);
    $long_range = movecoord($center, -12, 0, 12);
    $seq = mcannon_n_turn;
}

// Check close range
if (npc_hunt($close_range, 1, 1) = true) {
    if (lineofsight($center, npc_coord) = true) {
        $fired = true;
        gosub(cannon_fire);
    }
}

// Check med range
if ($fired = false & npc_hunt($medium_range, 2, 1) = true) {
    if (lineofsight($center, npc_coord) = true) {
        $fired = true;
        gosub(cannon_fire);
    }
}

// Check long range
if ($fired = false & npc_hunt($long_range, 5, 1) = true) {
    if (lineofsight($center, npc_coord) = true) {
        $fired = true;
        gosub(cannon_fire);
    }
}
loc_anim($seq);
if(gettimer(cannon_rotate) > 0) settimer(cannon_rotate, 1, modulo(add($dir, 1), 8));

[proc,cannon_fire]
if (%rockthrower < 1) {
    return;
}
if(npc_type = ikov_firewarrior) { // separate from attackable check
    ~chatnpc("<p,angry>Your puny weapons do nothing against me human! Come back when you can give me a real fight!");
    return;
}
if(npc_type = priestperilguarddog) {
    return;
}
if (~npc_is_attackable = false) {
    return;
}
if (~player_in_combat_check = false) { // https://youtu.be/TeQXQDaawO0?t=247
    return;
}
// map_projanim	id=53, starttime=0, endtime=40, angle=2, progress=11, startheight=36, endheight=35
// (coord $coord, npc_uid $uid, spotanim $spotanim, int $startheight, int $endheight, int $delay, int $angle, int $length, int $offset, int $step)(int)
def_int $duration = ~npc_projectile(movecoord(%ownedmcannon, 1, 0, 1), npc_uid, cannonball_travel, 36, 35, 0, 2, 35, 0, 5);
%rockthrower = sub(%rockthrower, 1);
~sound_within_distance(mcannon_fire, 0, %ownedmcannon, 10);
def_int $damage = 0;
if (~player_npc_hit_roll(%damagetype) = true) {
    $damage = randominc(min(30, npc_param(max_dealt)));
    def_int $damage_capped = min($damage, npc_stat(hitpoints));
    stat_advance(ranged, multiply(scale(%npc_combat_xp_multiplier, 1000, scale(200, 100, $damage_capped)), 10));
    npc_heropoints($damage_capped);
}

~npc_retaliate(divide($duration, 30));
npc_queue(2, $damage, divide($duration, 30)); // ai_queue2 takes uncapped damage (avoids npcs from tick healing)

if (npc_type = king_dragon) {
    ~kbd_destroy_cannon;
}
// https://web.archive.org/web/20040821134749/http://www.runescapecommunity.com/index.php?showtopic=159464&amp
// "I would add in that if you take it to the King Black Dragon it explodes and cannot be used."
// https://youtu.be/-wgfwyeRSmA?t=117
[proc,kbd_destroy_cannon]
if (random(4) = 0 & %ownedmcannon ! null) { // guess
// (coord $coord, coord $coord2, spotanim $spotanim, int $startheight, int $endheight, int $delay, int $angle, int $length, int $offset, int $step)(int)
    def_int $duration = ~coord_projectile(npc_coord, %ownedmcannon, firebreath_travel, 92, 31, 51, 16, -5, 64, 10);
    spotanim_map(fireblast_impact, %ownedmcannon, 124, $duration);
    npc_anim(dragon_firebreath_middle_attack, 0);
    sound_synth(dragonbreath, 0, 30);

    if (loc_find(%ownedmcannon, dwarf_multicannon1) = true) {
        mes("Your cannon has been destroyed!");
        loc_del(1);
        cleartimer(cannon_rotate);
    }
    
}
