[ai_opplayer2,ghast_invis]
if (%death = ^true) {
    npc_setmode(null);
    return;
}
if (%npc_action_delay > map_clock) return;
if (~npc_check_notcombat = false) {
    npc_setmode(null);
    return;
}
if (~npc_check_notcombat_self = false) { // Doesnt change mode! Keeps trying!
    return;
}
if (add(%ghast_delay, 26) > map_clock) { // 26t delay between being attacked by ghasts
    return;
}
if_close;
~npc_set_attack_vars;
npc_anim(inv_ghast_attack, 0);
sound_synth(ghast_attack, 0, 0);
def_int $rand = random(10);
%npc_action_delay = add(map_clock, 4); 
%ghast_delay = map_clock;
if(inv_total(inv, druid_pouch) > 0) {
    @druid_pouch_activate;
}
if($rand < 3) {
    npc_say("Oouuaarrr!");
    if(~ghast_check_food = false) {
        mes("A supernatural force draws energy from you.");
        queue(damage_player, 0, ~random_range(1,3)); // this doesnt put you in combat
    }
} else {
    npc_say("OOooooohhh");
    mes("An attacking Ghast just misses you.");
}
npc_setmode(null);

[opnpcu,ghast_invis]
if(last_useitem = druid_pouch) {
    if (~player_in_combat_check = false) {
        return;
    }
    @druid_pouch_activate;
}

[opnpc2,ghast_vis]
if (%druidspirit < ^druidspirit_added_pouch) {
    mes("You need to make your own druid pouch to attack Ghasts.");
    return;
}
@player_combat_start;

// you can manual cast to kill them @ w/e stage
[apnpc2,ghast_vis] 
if (%druidspirit < ^druidspirit_added_pouch) {
    mes("You need to make your own druid pouch to attack Ghasts.");
    return;
}
@player_combat_start_ap;

[ai_opplayer2,ghast_vis]
if (%death = ^true) {
    npc_setmode(null);
    return;
}
if (%npc_action_delay > map_clock) return;
if (~npc_check_notcombat = false) {
    npc_setmode(null);
    return;
}
if (~npc_check_notcombat_self = false) { // Doesnt change mode! Keeps trying!
    return;
}
if (add(%ghast_delay, 26) > map_clock & %aggressive_npc ! null & %aggressive_npc ! npc_uid) { // 26t delay between being attacked by ghasts
    return;
}
%ghast_delay = map_clock;
anim(%com_defendanim, 0);
npc_anim(npc_param(attack_anim), 0);
if (npc_param(attack_sound) ! null) {
    sound_synth(npc_param(attack_sound), 0, 0);
}
~npc_meleeattack;

[ai_queue3,ghast_vis]
// only counts for last hit & most heropts, otherwise xp is lost
if(npc_findhero = ^true & uid = %npc_aggressive_player & finduid(%npc_aggressive_player) = true) queue(ghast_vis_reward, 0, 0);
// npc_walk(npc_coord) is unnecessary due to npc_setmode(none).
// But they do it in this screenshot so might be good to do it here just incase: https://i.imgur.com/AfQ1MfF.png
npc_walk(npc_coord);
npc_setmode(none);
npc_arrivedelay; // arrivedelay for up to two ticks (current osrs too) https://youtu.be/Vsnkdq8OOMg?t=209
if (finduid(%npc_aggressive_player) = true) {
    if (npc_param(death_sound) ! null) {
        ~sound_within_distance(npc_param(death_sound), 0, npc_coord, 12); // osrs
    }
    %lastcombat = null; // allows other npcs to hunt you immediately
}
npc_anim(npc_param(death_anim), 0);
spotanim_npc(ghast_spotdeath, 0, 0);
npc_delay(1); // osrs has an extra tick of delay here. I think this delay can vary from npc to npc
npc_del;
if(npc_findhero = ^false) {
    return;
}
if_close;
// they dont drop bones, idk why the wiki says they do
def_int $random = random(128);

if ($random < 3) obj_add(npc_coord, iron_mace, 1, ^lootdrop_duration);
else if ($random < 5) obj_add(npc_coord, iron_dagger, 1, ^lootdrop_duration);
else if ($random < 6) obj_add(npc_coord, bronze_kiteshield, 1, ^lootdrop_duration);
else if ($random < 9) obj_add(npc_coord, airrune, 4, ^lootdrop_duration);
else if ($random < 11) obj_add(npc_coord, bodyrune, 3, ^lootdrop_duration);
else if ($random < 12) obj_add(npc_coord, chaosrune, 4, ^lootdrop_duration);
else if ($random < 13) obj_add(npc_coord, cosmicrune, 2, ^lootdrop_duration);    
else if ($random < 14) obj_add(npc_coord, firerune, 7, ^lootdrop_duration);
else if ($random < 47) obj_add(npc_coord, ~randomherb, ^lootdrop_duration);
else if ($random < 68) obj_add(npc_coord, coins, 18, ^lootdrop_duration);
else if ($random < 78) obj_add(npc_coord, coins, 10, ^lootdrop_duration);
else if ($random < 86) obj_add(npc_coord, coins, 26, ^lootdrop_duration);
else if ($random < 92) obj_add(npc_coord, coins, 35, ^lootdrop_duration);
else if ($random < 94) obj_add(npc_coord, coins, 1, ^lootdrop_duration);
else if ($random < 120) obj_add(npc_coord, fishing_bait, 7, ^lootdrop_duration);
else if ($random < 122) obj_add(npc_coord, tinderbox, 1, ^lootdrop_duration);
else if ($random < 123) obj_add(npc_coord, eye_of_newt, 1, ^lootdrop_duration);
else if ($random < 124) obj_add(npc_coord, tin_ore, 1, ^lootdrop_duration);
else if ($random < 125) obj_add(npc_coord, ~randomjewel, ^lootdrop_duration);

[queue,ghast_vis_reward]
stat_advance(prayer, 300);
if(%druidspirit = ^druidspirit_added_pouch) {
    mes("That's one Ghast, 2 more to kill.");
    %druidspirit = ^druidspirit_killed_ghast1;
} else if(%druidspirit = ^druidspirit_killed_ghast1) {
    mes("That's two Ghasts, 1 more to kill.");
    %druidspirit = ^druidspirit_killed_ghast2;
} else if(%druidspirit = ^druidspirit_killed_ghast2) {
    mes("That's all three Ghasts!");
    %druidspirit = ^druidspirit_killed_ghast3;
}

[ai_timer,ghast_vis]
if(npc_getmode = none) {
    npc_changetype(ghast_invis, 500);
}

[proc,ghast_check_food]()(boolean)
def_int $slot = 0;
def_int $count = 0;
while ($slot < inv_size(inv)) {
    def_obj $slotobj = inv_getobj(inv, $slot);
    if ($slotobj ! null) {
        if(~is_food_item($slotobj) = true) {
           $count = add($count, 1);
        }
    }
    $slot = calc($slot + 1);
}

if($count = 0) {
    return (false);
}

$slot = 0;
def_obj $consumable;
def_int $rand_food = random($count);
while ($consumable = null & $slot < inv_size(inv)) {
    def_obj $slotobj = inv_getobj(inv, $slot);
    if ($slotobj ! null) {
        if(~is_food_item($slotobj) = true) {
            if($rand_food = 0) {
                $consumable = $slotobj;
            }
            $rand_food = sub($rand_food, 1);
        }
    }
    if($consumable = null) $slot = calc($slot + 1);
}
if($consumable = null) { // shouldnt hit this!
    return (false); 
}
inv_delslot(inv, $slot);
if (oc_category($consumable) = category_129) {
    inv_setslot(inv, $slot, piedish, 1);
}
inv_add(inv, rotten_food, 1);
mes("You feel something attacking your backpack, and smell a terrible stench.");
return (true);


[proc,is_food_item](obj $slotobj)(boolean)
if(oc_category($slotobj) = category_5 | oc_category($slotobj) = category_58 | oc_category($slotobj) = category_59 | oc_category($slotobj) = category_86 | $slotobj = rottenapples |
    oc_category($slotobj) = category_120 | oc_category($slotobj) = category_131 | $slotobj = pineapple | $slotobj = kebab | $slotobj = ugthanki_kebab_bad | $slotobj = ugthanki_kebab |
    $slotobj = curry | $slotobj = stew | oc_category($slotobj) = gnome_crunchies | oc_category($slotobj) = gnome_unf_odd_crunchies | oc_category($slotobj) = gnome_batta |
    oc_category($slotobj) = gnome_unf_odd_batta | oc_category($slotobj) = gnome_bowl | oc_category($slotobj) = gnome_unf_odd_bowl |
    oc_category($slotobj) = gnome_premade_and_toadlegs_kingworm | oc_category($slotobj) = category_129 | oc_category($slotobj) = category_132) {
    return (true);
}
return (false);

[label,druid_pouch_activate]
mes("The druid pouch makes the Ghast visible.");
inv_del(inv, druid_pouch, 1);
if(inv_total(inv, druid_pouch) = 0) {
    mes("You have nothing left in your druid pouch!");
    inv_add(inv, druid_pouch_empty, 1);
}
spotanim_npc(druidpouch_impact, 124, 90);
~npc_projectile(coord, npc_uid, druid_shooting_star, 43, 31, 51, 16, -5, 64, 10);
npc_delay(1);
npc_changetype(ghast_vis, 500);
%npc_attacking_uid = uid;
%aggressive_npc = npc_uid;

