[oploc1,loc_2614] @quest_vampire_coffin_open;
[oploc1,vampcoffinopen] @quest_vampire_coffin_search;
[oploc2,vampcoffinopen] @quest_vampire_coffin_close;

[proc,quest_vampire_set_progress](int $stage)
%vampire = $stage;
~send_quest_progress(questlist:vampire, %vampire, ^vampire_complete);

[label,quest_vampire_coffin_open]
// Temp note: dur does not need updated ?? nice even number
loc_change(vampcoffinopen, 10);
if (%vampire = ^vampire_complete | %vampire = ^quest_vampire_not_started) {
    return;
}
// cant open during combat in osrs, but this might be after rework?

if (%vampire > ^quest_vampire_not_started) {
    npc_findallany(0_48_152_6_46, 6, ^vis_off);
    while (npc_findnext = true) {
        if (npc_type = count_draynor & %npc_attacking_uid = uid) {
            return;
        }
    }
}

p_delay(4);
npc_add(0_48_152_6_46, count_draynor, 500);
%npc_attacking_uid = uid;

npc_sethuntmode(aggressive_melee_extra);
npc_sethunt(5);

[label,quest_vampire_coffin_close]
// Temp note: dur does not need updated ??? bc it's a good number
loc_change(loc_2614, 10);

[label,quest_vampire_coffin_search]
p_delay(0);
~mesbox("There's only a pillow in here..."); // https://youtu.be/LfUTWKG5Ey4?t=149, also similar to rsc

[queue,quest_vampire_complete]
stat_advance(attack, 48250);
~quest_vampire_set_progress(^vampire_complete);
session_log(^log_adventure, "Quest complete: Vampire Slayer");
~send_quest_complete(questlist:vampire, stake, 250, ^vampire_questpoints, "You have completed the\\nVampire Slayer Quest!");
