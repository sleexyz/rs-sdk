[oploc1,haunteddoorl] @open_manor_entrance(^left);
[oploc1,haunteddoorr] @open_manor_entrance(^right);

[label,open_manor_entrance](int $side)
if(coordz(coord) > coordz(loc_coord)) {
    mes("The doors won't open.");
    return;
}
~open_and_close_double_door2(~check_axis(coord, loc_coord, loc_angle), $side, door_open);
p_delay(0);
mes("The doors slam shut behind you.");

[oploc1,hauntedcompostheap]
~mesbox("I'm not looking through that with my hands!");

[oplocu,hauntedcompostheap]
if(last_useitem ! spade) {
    ~displaymessage(^dm_default);
    return;
}
anim(human_dig, 0);
mes("You dig through the compost heap.");
p_delay(2);
if(inv_total(inv, closet_key) = 0 & %haunted < ^haunted_complete) {
    mes("You find a small key.");
    inv_add(inv, closet_key, 1);
    return;
}
mes("You find nothing of interest.");

[oploc1,closet_door]
mes("The door is locked.");
sound_synth(locked, 0, 0);

[oplocu,closet_door]
if(last_useitem ! closet_key) {
    ~displaymessage(^dm_default);
    return;
}
@ernest_open_key_door;

[label,ernest_open_key_door]
mes("You unlock the door.");
mes("You go through the door.");
sound_synth(locked, 0, 0);
~open_and_close_door2(desertdoor_inactive, ~check_axis(coord, loc_coord, loc_angle), door_open);

[oploc1,hauntedfountain]
anim(human_pickpocket, 0);
sound_synth(fish_swim, 0, 0);
p_delay(0);
if(inv_total(inv, pressure_gauge) > 0 | %haunted = ^haunted_complete) {
    ~chatplayer("<p,neutral>It's full of dead fish!");
    return;
}
~chatplayer("<p,quiz>There seems to be a pressure gauge in here...");
if(%haunted_manor_fountain_poisoned = 1) {
    ~chatplayer("<p,neutral>... and a lot of dead fish.");
    mes("You get the pressure gauge from the fountain.");
    inv_add(inv, pressure_gauge, 1);
} else {
    mes("Something in the water bites you.");
    say("Ow!");
    ~damage_self(1);
    p_delay(0);
    ~chatplayer("<p,confused>... and a lot of piranhas!|I can't get the gauge out.");
}

[opheldu,poison]
if(last_useitem = fish_food) @poison_fish_food;

[opheldu,fish_food]
if(last_useitem = poison) @poison_fish_food;

[label,poison_fish_food]
inv_del(inv, fish_food, 1);
inv_del(inv, poison, 1);
inv_add(inv, poisoned_fish_food, 1);
mes("You poison the fish food.");

[oplocu,hauntedfountain]
if(last_useitem ! poisoned_fish_food) { 
    ~displaymessage(^dm_default);
    return;
}
anim(human_pickpocket, 0);
inv_del(inv, poisoned_fish_food, 1);
mes("You pour the poisoned fish food into the fountain.");
p_delay(1);
mes("The piranhas start eating the food...");
p_delay(2);
mes("... then die and float to the surface.");
%haunted_manor_fountain_poisoned = 1;

[oploc1,_manor_bookcase]
if(coordx(coord) < coordx(loc_coord)) {
    return;
}
mes("You've found a secret door!");
@manor_bookcase_door(loc_param(end_coord));

[oploc1,hauntedleverup]
p_delay(0);
facesquare(movecoord(coord, 1, 0, 0));
mes("The lever opens the secret door!");
anim(human_leverdown, 0);
sound_synth(lever, 0, 0);
// Temp note: dur updated
loc_change(hauntedleverdown, 4);
p_teleport(movecoord(coord, 0, 0, 1));
p_delay(1);
@manor_bookcase_door(movecoord(coord, 2, 0, 0));

// todo: This is borked for opening the doors now. Using the lever is fine.
// Gotta think through how to make this work with find all zone.
[label,manor_bookcase_door](coord $dest)
def_coord $start_coord = coord;
p_delay(0);
// If you search from the north end of the northern bookcase, you will be in a different zone
// than the bookcases, this is a small workaround
loc_findallzone(movecoord(coord, 0, 0, -1));
while (loc_findnext = true) {
    if(loc_type = hauntedbookcasel) {
        // Temp note: dur updated
        loc_change(inviswall, 3);
        loc_add(movecoord(loc_coord, 0, 0, -1), hauntedbookcaseopen, loc_angle, loc_shape, 3);
    } else if(loc_type = hauntedbookcaser) {
        // Temp note: dur updated
        loc_change(inviswall, 3);
        loc_add(movecoord(loc_coord, 0, 0, 1), hauntedbookcaseopen, loc_angle, loc_shape, 3);
        sound_synth(coffin_open, 0, 0);
    }
}
p_teleport($dest);

[oploc1,puzzle_ladder_top]
~reset_haunted_levers;
~climb_ladder(0_48_152_44_26, false);

[oploc1,puzzle_ladder]
~reset_haunted_levers;
~climb_ladder(0_48_52_20_33, true);

[proc,reset_haunted_levers]
%ernestlever = clearbit_range(%ernestlever, 0, 5);

[oploc1,_haunted_lever]
p_delay(0);
def_string $dir = "down";
def_int $name_idx = string_length(loc_name);
if(testbit(%ernestlever, loc_param(lever_index)) = ^true) {
    $dir = "up";
}
%ernestlever = togglebit(%ernestlever, loc_param(lever_index));
anim(human_leverdown, 0); // OSRS uses seq's not in 225, going to assume here
sound_synth(lever, 0, 0);
mes("You pull lever <substring(loc_name, calc($name_idx - 1), $name_idx)> <$dir>.");
mes("You hear a clunk.");

[oploc2,_haunted_lever]
def_string $dir = "up";
if(testbit(%ernestlever, loc_param(lever_index)) = ^true) {
    $dir = "down";
}
mes("The lever is <$dir>.");

[oploc1,_haunted_door]
if((loc_type = 1to2 & (testbit(%ernestlever, 0) = ^false & testbit(%ernestlever, 1) = ^false & testbit(%ernestlever, 3) = ^true & testbit(%ernestlever, 4) = ^true & testbit(%ernestlever, 5) = ^true))
    | (loc_type = 2to3 & (testbit(%ernestlever, 1) = ^false & testbit(%ernestlever, 3) = ^true & testbit(%ernestlever, 5) = ^true))
    | (loc_type = 4to5 & (testbit(%ernestlever, 0) = ^true & testbit(%ernestlever, 1) = ^true & testbit(%ernestlever, 3) = ^true))
    | (loc_type = 5to6 & testbit(%ernestlever, 3) = ^true)
    | (loc_type = 8to9 & (testbit(%ernestlever, 4) = ^false & testbit(%ernestlever, 5) = ^true))
    | (loc_type = 2to5 & (testbit(%ernestlever, 0) = ^false & testbit(%ernestlever, 1) = ^false & testbit(%ernestlever, 2) = ^true & testbit(%ernestlever, 3) = ^true & testbit(%ernestlever, 4) = ^false & testbit(%ernestlever, 5) = ^true))
    | (loc_type = 3to6 & (testbit(%ernestlever, 1) = ^false & testbit(%ernestlever, 3) = ^true & testbit(%ernestlever, 5) = ^false))
    | (loc_type = 4to7 & (testbit(%ernestlever, 0) = ^true & testbit(%ernestlever, 1) = ^true & testbit(%ernestlever, 2) = ^false & testbit(%ernestlever, 3) = ^false & testbit(%ernestlever, 4) = ^false & testbit(%ernestlever, 5) = ^false))
    | (loc_type = 5to8 & ((testbit(%ernestlever, 2) = ^false & testbit(%ernestlever, 3) = ^true) | (testbit(%ernestlever, 0) = ^false & testbit(%ernestlever, 1) = ^false & testbit(%ernestlever, 2) = ^true & testbit(%ernestlever, 3) = ^true & testbit(%ernestlever, 4) = ^false & testbit(%ernestlever, 5) = ^true)))) {
    ~open_and_close_door2(poordoor, ~check_axis(coord, loc_coord, loc_angle), door_open);
    return;
}
mes("This door is locked.");
sound_synth(locked, 0, 0);

[proc,change_ernest]
if(npc_find(coord, ernest_the_chicken, 8, 0) = true) {
    npc_changetype_keepall(ernest, 100);
}

[queue,haunted_quest_complete]
session_log(^log_adventure, "Quest complete: Ernest the Chicken");
~send_quest_complete(questlist:haunted, feather, 250, ^haunted_questpoints, "You have completed the\\nErnest The Chicken Quest!");
inv_add(inv, coins, 300);
