[opheld1,elemental_workshop_shield_book]
%book_page = 0;
%open_book = elemental_workshop_shield_book;
if(testbit(%elemental_workshop_bits, ^elemental_workshop_read_book) = ^false) {
    %elemental_workshop_bits = setbit(%elemental_workshop_bits, ^elemental_workshop_read_book);
    ~send_quest_progress(questlist:elemental_workshop, %elemental_workshop_bits, multiply(2, ^elemental_workshop_complete));
}
mes("The book has two parts an introduction and an instruction section.");
mes("You flip the book open to the introduction and start reading.");
@open_elemental_workshop_shield_book;

[opheldu,elemental_workshop_shield_book]
if(testbit(%elemental_workshop_bits, ^elemental_workshop_read_book) = ^true & inv_total(inv, elemental_workshop_key) = 0 & (last_useitem = knife | oc_param(last_useitem, slashattack_anim) ! human_unarmedpunch)) { 
    mes("You make a small cut in the spine of the book.");
    // https://web.archive.org/web/20041214093302im_/http://img.photobucket.com/albums/v289/MrFlibble/ScreenHunter_038.bmp
    mes("Inside you find a small old battered key.");
    // no slashed book, don't think it deletes cause you need book to make shield
    inv_add(inv, elemental_workshop_key, 1);
    if(testbit(%elemental_workshop_bits, ^elemental_workshop_slashed_book) = ^false) {
        %elemental_workshop_bits = setbit(%elemental_workshop_bits, ^elemental_workshop_slashed_book);
    }
    return;
}
mes("You don't want to damage the book.");

[label,open_elemental_workshop_shield_book]
@book_flip_page(0, 0, 1, elemental_shield_book);

[proc,elemental_shield_book]
switch_int (%book_page) {
    case 0 : ~book("Book of the elemental shield", "Within the pages of this book you will find the secret to working the very elements themselves. Early in the fifth age, a new ore was discovered. This ore has a unique property of absorbing, transforming or focusing elemental energy. A workshop was erected close by to work this new material. The workshop was set up for artisans and inventors to be able to come and create devices made from the unique ore, found only in the village of the Seer's.");
    case 1 : ~book("Book of the elemental shield", "After some time of successful industry the true power of this ore became apparent, as greater and more powerful weapons were created. Realising the threat this posed, the magi of the time closed down the workshop and bound it under lock and key, also trying to destroy all knowledge of manufacturing processes. Yet this book remains and you may still find a way to enter the workshop within this leather bound volume.");
}
