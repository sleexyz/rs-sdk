[oploc2,loc_2650]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(%murder_poisonproof_progress >= ^poisonproof_spoken_murderer) {
    if(%murdersus = ^murderer_anna) {
        // https://youtu.be/K8nWqTSdyis?si=6FT4682PaDltSu1Y&t=85
        %murder_poisonproof_progress = ^poisonproof_searched_loc;
        ~mesbox("The compost is teeming with maggots.|Somebody should really do something about it.|It's certainly clear nobody's used poison here.");
    } else {
        ~mesbox("There is a faint smell of poison behind the smell of the compost.");
    }
    return;
}
mes("It's a heap of compost.");

[oploc2,quest_murder_beehive]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(%murder_poisonproof_progress >= ^poisonproof_spoken_murderer) {
    if(%murdersus = ^murderer_bob) {
        // https://youtu.be/5CH-NF3kY-c?si=kE4dRh4Dy0PJmxVl&t=342
        %murder_poisonproof_progress = ^poisonproof_searched_loc;
        ~mesbox("The beehive buzzes with activity.|These bees definitely don't seem poisoned at all.");
    } else {
        ~mesbox("The hive is empty. There are a few dead bees|and a faint smell of poison.");
    }
    return;
}
mes("It's a very old beehive.");

[oploc2,murderdrain]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(%murder_poisonproof_progress >= ^poisonproof_spoken_murderer) {
    if(%murdersus = ^murderer_carol) {
        // https://youtu.be/zGE4VgOtfaI?si=SzW4-7G_8e_5fLPp&t=404
        %murder_poisonproof_progress = ^poisonproof_searched_loc;
        ~mesbox("The drain is totally blocked.|It really stinks. No, it REALLY smells bad.|It's certainly clear nobody's cleaned it recently.");
    } else {
        ~mesbox("The drain seems to have been recently cleaned.|You can still smell the faint aroma of poison.");
    }
    return;
}
mes("It's the drains from the kitchen.");

[oploc2,loc_2653]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(%murder_poisonproof_progress >= ^poisonproof_spoken_murderer) {
    if(%murdersus = ^murderer_david) {
        // https://youtu.be/P1IDGCA2f9o?si=6aKZAUVbGoVmUF8t&t=327
        %murder_poisonproof_progress = ^poisonproof_searched_loc;
        ~mesbox("There is a spiders' nest here. You estimate there must be at least a few hundred spiders ready to hatch. It's certainly clear nobody's used poison here.");
    } else {
        ~mesbox("A faint smell of poison and a few dead spiders|is all that remains of the spiders nest.");
    }
    return;
}
mes("It looks like a spiders' nest of some kind...");

[oploc2,murderfountain]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(%murder_poisonproof_progress >= ^poisonproof_spoken_murderer) {
    if(%murdersus = ^murderer_elizabeth) {
        // https://youtu.be/P1IDGCA2f9o?si=6aKZAUVbGoVmUF8t&t=327
        ~mesbox("The fountain is swarming with mosquitos.|There's a nest of them underneath the fountain.");
        ~chatplayer("<p,angry>I hate mosquitos, they're so annoying!");
        // not 100% sure if the varp is set at this point, the other locs should all be correct
        %murder_poisonproof_progress = ^poisonproof_searched_loc;
        ~mesbox("It's certainly clear nobody's used poison here.");
    } else {
        ~mesbox("There are a lot of dead mosquitos around|the base of the fountain. A faint smell of|poison is in the air, but the water seems clean.");
    }
    return;
}
mes("A fountain with large numbers of insects around the base.");

[oploc2,murdersign]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(%murder_poisonproof_progress >= ^poisonproof_spoken_murderer) {
    if(%murdersus = ^murderer_frank) {
        %murder_poisonproof_progress = ^poisonproof_searched_loc;
        ~mesbox("It looks like the Sinclair family crest|but it is very dirty.|You can barely make it out under all of the grime.|It's certainly clear nobody's cleaned it recently.");
    } else {
        ~mesbox("The Sinclair family crest.|It's shiny and freshly polished|and has a slight smell of poison.");
    }
    return;
}
mes("The Sinclair Family Crest is hung up here.");

[oploc2,murderbarrela]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(~obj_gettotal(murdernecklace) > 0) {
    mes("I already have Anna's Necklace.");
    return;
}
~mesbox("There's something shiny hidden at the bottom.|You take Anna's Silver Necklace.");
inv_add(inv, murdernecklace, 1);

[oploc2,murderbarrelb]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(~obj_gettotal(murdercup) > 0) {
    mes("I already have Bob's cup.");
    return;
}
~mesbox("There's something shiny hidden at the bottom.|You take Bob's silver cup.");
inv_add(inv, murdercup, 1);

[oploc2,murderbarrelc]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(~obj_gettotal(murderbottle) > 0) {
    mes("I already have Carol's bottle.");
    return;
}
~mesbox("There's something shiny hidden at the bottom.|You take Carol's silver bottle.");
inv_add(inv, murderbottle, 1);

[oploc2,murderbarreld]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(~obj_gettotal(murderbook) > 0) {
    mes("I already have David's book.");
    return;
}
~mesbox("There's something shiny hidden at the bottom.|You take David's silver book.");
inv_add(inv, murderbook, 1);

[oploc2,murderbarrele]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(~obj_gettotal(murderneedle) > 0) {
    mes("I already have Elizabeth's needle.");
    return;
}
~mesbox("There's something shiny hidden at the bottom.|You take Elizabeth's silver needle.");
inv_add(inv, murderneedle, 1);

[oploc2,murderbarrelf]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(~obj_gettotal(murderpot) > 0) {
    mes("I already have Frank's pot.");
    return;
}
~mesbox("There's something shiny hidden at the bottom.|You take Frank's silver pot.");
inv_add(inv, murderpot, 1);

[label,murder_flour_barrel]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
if(inv_total(inv, pot_empty) = 0) {
    mes("A barrel full of finely sifted flour.");
    mes("You need something to put the flour in.");
    return;
}
inv_del(inv, pot_empty, 1);
inv_add(inv, pot_flour, 1);
mes("You take some flour from the barrel.");
mes("There's still plenty of flour left.");

[oploc2,loc_2662] @murder_flour_barrel;

[oplocu,loc_2662]
if(last_useitem = murderpot2) {
    ~mesbox("You probably shouldn't use evidence from a crime|scene to keep flour in...");
    return;
}
if(last_useitem = pot_empty) {
    @murder_flour_barrel;
}
// no nothing interesting happens

[oploc2,murdersacks]
if(%murderquest ! ^murder_started) {
    mes("I need the guards' permission to do that.");
    return;
}
~mesbox("There's some flypaper in there.|Should I take it?");
def_int $choice = ~p_choice2("Yes, it might be useful.", 1, "No, I don't see any need for it.", 2);
if($choice = 2) {
    return;
}
inv_add(inv, murderpaper, 1);
~mesbox("You take a piece of fly paper.|There is still plenty of fly paper left.");

[oploc1,murderdoggatel] @murder_inspect_gate;
[oploc1,murderdoggater] @murder_inspect_gate;

[label,murder_inspect_gate]
if(%murderquest ! ^murder_started) {
    // the punc here is intentionally different
    mes("I need the guard's permission to do that.");
    return;
}
~mesbox("As you approach the gate the Guard Dog starts barking loudly at you. There is no way an intruder could have committed the murder. It must have been someone the dog knew to get past it quietly.");

[oploc2,loc_2666]
p_arrivedelay;
// after quest completion, osrs sends the mes "A slightly broken window. Could be easily shattered.", this was probably added with
// kings ransom since the break op doesn't exist until then (and the mes doesn't exist in RSC)
if(%murderquest ! ^murder_started) {
    mes("You need the guards' permission to do that.");
    return;
}
~mesbox("Some thread seems to have been caught on a loose nail on the window.");
if(~obj_gettotal(~get_murder_thread) > 0) {
    ~mesbox("You have already taken the thread.");
    return;
}
if(testbit(%murder_evidence, ^murder_found_thread) = ^true) {
    ~mesbox("Lucky for you there's some thread left.|You should be less careless in future.");
    inv_add(inv, ~get_murder_thread, 1);
    return;
}
%murder_evidence = setbit(%murder_evidence, ^murder_found_thread);
mes("You take the thread.");
inv_add(inv, ~get_murder_thread, 1);

[proc,get_murder_thread]()(namedobj)
switch_int(%murdersus) {
    case ^murderer_anna, ^murderer_david : return (murderthreadg);
    case ^murderer_bob, ^murderer_carol : return (murderthreadr);
    case ^murderer_elizabeth, ^murderer_frank : return (murderthreadb);
    case default : return (murderthreadg); // fallback but this should never happen
}

[opobj3,murderweapon]
// no map_members check
if(%murderquest = ^murder_complete) {
    ~mesbox("I cannot take the flimsy dagger.|The Guards will need it as evidence.");
    return;
}
if(inv_total(inv, murderweapon) > 0) {
    mes("I already have the murder weapon.");
    return;
}
if(inv_total(bank, murderweapon) > 0) {
    mes("I already have the murder weapon in my bank.");
    return;
}
inv_add(inv, murderweapon, 1);
~mesbox("This knife doesn't seem sturdy enough to have killed Lord Sinclair.");

[opobj3,murderpot2]
// no map_members check
if(%murderquest = ^murder_complete) {
    ~mesbox("I cannot take the poisoned pot.|The Guards will need it for evidence.");
    return;
}
if(inv_total(inv, murderpot2) > 0) {
    mes("I already have the poisoned pot.");
    return;
}
if(inv_total(bank, murderpot2) > 0) {
    mes("I already have the poisoned pot in my bank.");
    return;
}
inv_add(inv, murderpot2, 1);
~mesbox("It seems like Lord Sinclair was drinking from this before he died.");

[opheldu,_murder_proofobj]
if(last_item = pot_flour) {
    @flour_proofobj(last_useitem);
}
~displaymessage(^dm_default);

[opheldu,_murder_flourobj]
if(last_item = murderpaper) {
    @create_flourprints(last_useitem);
}
~displaymessage(^dm_default);

[opheldu,murderfingerprint1]
if(oc_category(last_useitem) = murder_printobj) {
    @check_murderer_print(last_useitem);
}
~displaymessage(^dm_default);

[label,flour_proofobj](obj $proof_obj)
inv_del(inv, $proof_obj, 1);
inv_add(inv, oc_param($proof_obj, product), 1);
inv_del(inv, pot_flour, 1);
inv_add(inv, pot_empty, 1);
mes(oc_param($proof_obj, game_message));
mes(oc_param($proof_obj, game_message2));

[label,create_flourprints](obj $flour_obj)
inv_del(inv, $flour_obj, 1);
inv_add(inv, oc_param($flour_obj, next_obj_stage), 1);
inv_del(inv, murderpaper, 1);
inv_add(inv, oc_param($flour_obj, product), 1);
mes(oc_param($flour_obj, game_message));
mes(oc_param($flour_obj, game_message2));

[label,check_murderer_print](obj $checked_print)
def_string $print_name = oc_name($checked_print);
def_string $murderer_name = substring($print_name, 0, calc(string_indexof_char($print_name, ' ') - 2));
if(oc_param($checked_print, murder_id_value) = %murdersus) {
    mes("The finger prints are an exact match to <$murderer_name>'s.");
    %murder_evidence = setbit(%murder_evidence, ^murder_found_fingerprints);
    inv_del(inv, murderfingerprint1, 1);
    inv_add(inv, murderfingerprint, 1);
    return;
}
~mesbox("They don't seem to be the same.|I guess that clears <$murderer_name> of the crime.|You destroy the useless fingerprint.");
inv_del(inv, $checked_print, 1);

[opobj3,murderthreadb]
~mesbox("Some blue thread. Not enough to be worth taking.");

[opobj3,murderthreadr]
~mesbox("Some red thread. Not enough to be worth taking.");

[opobj3,murderthreadg]
~mesbox("Some green thread. Not enough to be worth taking.");

[proc,murder_get_murderer_name]()(string)
switch_int(%murdersus) {
    case 2 : return ("Bob");
    case 3 : return ("Carol");
    case 4 : return ("David");
    case 5 : return ("Elizabeth");
    case 6 : return ("Frank");
    case default : return ("Anna");
}

[proc,murder_clear_evidence]
// removes each of the related quest items from the player inv and bank
inv_del(inv, murdernecklace, ^max_32bit_int);
inv_del(bank, murdernecklace, ^max_32bit_int);
if(inv_total(worn, murdernecklace) > 0) {
    inv_del(worn, murdernecklace, 1);
    ~update_all(null);
}
inv_del(inv, murdernecklacedust, ^max_32bit_int);
inv_del(bank, murdernecklacedust, ^max_32bit_int);
if(inv_total(worn, murdernecklacedust) > 0) {
    inv_del(worn, murdernecklacedust, 1);
    ~update_all(null);
}
inv_del(inv, murdercup, ^max_32bit_int);
inv_del(bank, murdercup, ^max_32bit_int);
inv_del(inv, murdercupdust, ^max_32bit_int);
inv_del(bank, murdercupdust, ^max_32bit_int);
inv_del(inv, murderbottle, ^max_32bit_int);
inv_del(bank, murderbottle, ^max_32bit_int);
inv_del(inv, murderbottledust, ^max_32bit_int);
inv_del(bank, murderbottledust, ^max_32bit_int);
inv_del(inv, murderbook, ^max_32bit_int);
inv_del(bank, murderbook, ^max_32bit_int);
inv_del(inv, murderbookdust, ^max_32bit_int);
inv_del(bank, murderbookdust, ^max_32bit_int);
inv_del(inv, murderneedle, ^max_32bit_int);
inv_del(bank, murderneedle, ^max_32bit_int);
inv_del(inv, murderneedledust, ^max_32bit_int);
inv_del(bank, murderneedledust, ^max_32bit_int);
inv_del(inv, murderpot, ^max_32bit_int);
inv_del(bank, murderpot, ^max_32bit_int);
inv_del(inv, murderpotdust, ^max_32bit_int);
inv_del(bank, murderpotdust, ^max_32bit_int);
inv_del(inv, murderthreadr, ^max_32bit_int);
inv_del(bank, murderthreadr, ^max_32bit_int);
inv_del(inv, murderthreadg, ^max_32bit_int);
inv_del(bank, murderthreadg, ^max_32bit_int);
inv_del(inv, murderthreadb, ^max_32bit_int);
inv_del(bank, murderthreadb, ^max_32bit_int);
inv_del(inv, murderpaper, ^max_32bit_int);
inv_del(bank, murderpaper, ^max_32bit_int);
inv_del(inv, murderpot2, ^max_32bit_int);
inv_del(bank, murderpot2, ^max_32bit_int);
inv_del(inv, murderweapon, ^max_32bit_int);
inv_del(bank, murderweapon, ^max_32bit_int);
inv_del(inv, murderweapondust, ^max_32bit_int);
inv_del(bank, murderweapondust, ^max_32bit_int);
inv_del(inv, murderfingerprint, ^max_32bit_int);
inv_del(bank, murderfingerprint, ^max_32bit_int);
inv_del(inv, murderfingerprinta, ^max_32bit_int);
inv_del(bank, murderfingerprinta, ^max_32bit_int);
inv_del(inv, murderfingerprintb, ^max_32bit_int);
inv_del(bank, murderfingerprintb, ^max_32bit_int);
inv_del(inv, murderfingerprintc, ^max_32bit_int);
inv_del(bank, murderfingerprintc, ^max_32bit_int);
inv_del(inv, murderfingerprintd, ^max_32bit_int);
inv_del(bank, murderfingerprintd, ^max_32bit_int);
inv_del(inv, murderfingerprinte, ^max_32bit_int);
inv_del(bank, murderfingerprinte, ^max_32bit_int);
inv_del(inv, murderfingerprintf, ^max_32bit_int);
inv_del(bank, murderfingerprintf, ^max_32bit_int);
inv_del(inv, murderfingerprint1, ^max_32bit_int);
inv_del(bank, murderfingerprint1, ^max_32bit_int);

[queue,murder_quest_complete]
%murderquest = ^murder_complete;
%murder_poisonproof_progress = 0;
%murder_evidence = 0;
%murdersus = 0;
stat_advance(crafting, 14060);
inv_add(inv, coins, 2000);
session_log(^log_adventure, "Quest complete: Murder Mystery");
~send_quest_complete(questlist:murder, coins, 250, ^murder_questpoints, "You have completed the\\nMurder Mystery Quest!");
