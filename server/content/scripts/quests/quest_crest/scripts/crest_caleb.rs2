[opnpc1,caleb_fitzharmon]
switch_int(%crestquest) {
    case ^crest_not_started : @caleb_fitzharmon_prequest;
    case ^crest_spoken_dimintheis : @caleb_fitzharmon_start;
    case ^crest_spoken_caleb : @caleb_fitzharmon_fish;
    case ^crest_caleb_piece : @caleb_fitzharmon_salad;
    case ^crest_caleb_where : @caleb_fitzharmon_avan;
    case ^crest_complete : @caleb_fitzharmon_gauntlets;
    case default : @caleb_fitzharmon_pieces;
}

[label,caleb_fitzharmon_prequest]
~chatnpc("<p,quiz>Who are you? What are you after?");
@multi2("Nothing, I will be on my way.", caleb_fitzharmon_nothing, "I see you are a chef, will you cook me anything?", caleb_fitzharmon_chef);

[label,caleb_fitzharmon_start]
~chatnpc("<p,quiz>Who are you? What are you after?");
@multi3("Are you Caleb Fitzharmon?", caleb_fitzharmon_areyou, 
        "Nothing, I will be on my way.", caleb_fitzharmon_nothing, 
        "I see you are a chef, will you cook me anything?", caleb_fitzharmon_chef);

[label,caleb_fitzharmon_areyou]
~chatplayer("<p,quiz>Are you Caleb Fitzharmon?");
~chatnpc("<p,confused>Why... yes I am, but I don't believe I know you... how did you know my name?");
~chatplayer("<p,neutral>I have been sent by your father. He wishes the Fitzharmon Crest be restored.");
~chatnpc("<p,confused>Ah... well... hmmm... yes... I do have a piece of it anyway...");
@multi2("Uh... what happened to the rest of it?", caleb_fitzharmon_rest, "So can I have your bit?", caleb_fitzharmon_bit);

[label,caleb_fitzharmon_rest]
~chatplayer("<p,quiz>Uh... what happened to the rest of it?");
~chatnpc("<p,sad>Well... my brothers and I had a slight disagreement about it... we all wanted to be heir to my fathers' lands, and we each ended up with a piece of the crest.");
~chatnpc("<p,sad>None of us wanted to give up our rights to our brothers, so we didn't want to give up our pieces of the crest, but none of us wanted to face our father by");
~chatnpc("<p,sad>returning to him with an incomplete crest... We each went our seperate ways many years past, none of us seeing our father or willing to give up our fragments.");
// If the player doesn't have Caleb's piece yet, the dialogue ends here
// https://youtu.be/e8btuenT4nE?t=167
if (%crestquest = ^crest_caleb_piece) {
    ~chatplayer("<p,quiz>So do you know where I could find any of your brothers?");
    ~chatnpc("<p,neutral>Well, we haven't really kept in touch... what with the dispute over the crest and all... I did hear from my brother Avan a while ago though..");
    %crestquest = ^crest_caleb_where;
    ~chatnpc("<p,neutral>He said he was on some kind of search for treasure, or gold, or something out in the desert. You might want to ask around there for him.");
    ~chatnpc("<p,neutral>Avan always did have expensive tastes however. You may find he is not prepared to hand over his crest piece to you as easily as I have.");
}

[label,caleb_fitzharmon_bit]
~chatplayer("<p,quiz>So can I have your bit?");
~chatnpc("<p,neutral>Well, I am the oldest son, so by the rules of chivalry, I am most entitled to be rightful bearer of the crest.");
~chatplayer("<p,confused>It's not really much use without the other fragments is it though?");
~chatnpc("<p,sad>Well that is true... perhaps it is time to put my pride aside... I'll tell you what: I'm struggling to complete this fish salad of mine,");
~chatnpc("<p,neutral>so if you will assist me in my search for the ingredients, then I will let you take my piece as reward for your assistance.");
~chatplayer("<p,quiz>So what ingredients are you missing?");
~chatnpc("<p,neutral>I require the following cooked fish: Swordfish, Bass, Tuna, Salmon and Shrimp.");
def_int $op = ~p_choice2("Ok, I will get those.", 1, "Why don't you just give me the crest?", 2);
if ($op = 1) {
    %crestquest = ^crest_spoken_caleb;
    ~chatplayer("<p,neutral>Ok, I will get those.");
    ~chatnpc("<p,happy>You will? It would help me a lot!");
}
else {
    ~chatplayer("<p,quiz>Why don't you just give me the crest?");
    %crestquest = ^crest_spoken_caleb;
    ~chatnpc("<p,angry>It's a valuable family heirloom. I think the least you can do is prove you're worthy of it before I hand it over.");
}

[label,caleb_fitzharmon_fish]
~chatnpc("<p,quiz>How is the fish collecting going?");
if (inv_total(inv, swordfish) >= 1 & 
    inv_total(inv, bass) >= 1 & 
    inv_total(inv, tuna) >= 1 &
    inv_total(inv, salmon) >= 1 &
    inv_total(inv, shrimp) >= 1) {
        ~chatplayer("<p,neutral>Got them all with me.");
        ~mesbox("You exchange the fish for Caleb's piece of the crest.");
        %crestquest = ^crest_caleb_piece;
        inv_del(inv, swordfish, 1);
        inv_del(inv, bass, 1);
        inv_del(inv, tuna, 1);
        inv_del(inv, salmon, 1);
        inv_del(inv, shrimp, 1);
        inv_add(inv, avan_crest, 1);
        @multi2("Uh... what happened to the rest of it?", caleb_fitzharmon_rest, "Thank you very much!", caleb_fitzharmon_thanks);
    }
else {
    ~chatplayer("<p,sad>I didn't manage to get them all yet...");
    ~chatnpc("<p,neutral>Remember, I want the following cooked fish: Swordfish, Bass, Tuna, Salmon, and Shrimp.");
}

[label,caleb_fitzharmon_thanks]
~chatplayer("<p,happy>Thank you very much.");
~chatnpc("<p,happy>You're welcome.");

[label,caleb_fitzharmon_salad]
~chatnpc("<p,neutral>Hello again. I'm just putting the finishing touches to my masterful salad.");
@multi2("Uh... what happened to the rest of it?", caleb_fitzharmon_rest, "Good luck with that then.", caleb_fitzharmon_luck);

[label,caleb_fitzharmon_luck]
~chatplayer("<p,neutral>Good luck with that then.");
~chatnpc("<p,neutral>Luck? LUCK? With my cooking skill, I don't really need LUCK.");

[label,caleb_fitzharmon_avan]
~chatplayer("<p,quiz>Where did you say I could find Avan again?");
~chatnpc("<p,neutral>Last I heard he was on some stupid treasure hunt out in the desert somewhere. Your best bet is asking around there.");
@caleb_fitzharmon_pieces;

[label,caleb_fitzharmon_pieces]
~chatnpc("<p,quiz>How are you doing getting the crest pieces?");
if (inv_total(inv, family_crest) >= 1) {
    ~chatplayer("<p,neutral>I've got it right here!");
    ~chatnpc("<p,neutral>Excellent work! Please take it to my father immediately.");
}
else if (inv_total(inv, avan_crest) = 0 & inv_total(bank, avan_crest) = 0) {
    @multi2("I am still working on it.", caleb_fitzharmon_working, "I have lost the piece you gave me.", caleb_fitzharmon_lost);
}
else {
    @caleb_fitzharmon_working;
}

[label,caleb_fitzharmon_working]
~chatplayer("<p,neutral>I am still working on it.");
~chatnpc("<p,quiz>Then why are you wasting your time here?");

[label,caleb_fitzharmon_lost]
~chatplayer("<p,neutral>I have lost the fragment you gave me...");
~chatnpc("<p,confused>I have some good news for you then. One of my customers found this on their travels and recognised it as mine and returned it to me here.");
~mesbox("Caleb hands over his crest piece again.");
inv_add(inv, avan_crest, 1);
~chatnpc("<p,neutral>I suggest you be less careless in the future. The crest is extremely valuable, and utterly irreplaceable.");

[label,caleb_fitzharmon_nothing]
~chatplayer("<p,neutral>Nothing, I will be on my way.");

[label,caleb_fitzharmon_chef]
~chatplayer("<p,quiz>I see you are a chef... Could you cook me something?");
// "reknown" is verbatim
~chatnpc("<p,neutral>I would, but I am very busy. I am trying to increase my reknown as one of the worlds' leading chefs by preparing a special and unique fish salad.");

[label,caleb_fitzharmon_gauntlets]
// Ability to change gauntlets for 25k was added 30/08/05, so this decision is permanent
// https://oldschool.runescape.wiki/w/Update:Garden_Of_Tranquillity
~chatnpc("<p,happy>I hear you brought the completed crest to my father. I must say, that was awfully impressive work.");
if(getbit_range(%crest_spells_levers_gauntlets, ^crest_cooking_gauntlets_chosen, ^crest_chaos_gauntlets_chosen) > 0) {
    return;
}
if (inv_total(inv, steel_gauntlets) >= 1) {
    ~chatplayer("<p,neutral>I believe your father mentioned you would be able to improve these gauntlets for me...");
    ~chatnpc("<p,neutral>Yes, that is correct. I can slightly alter these gauntlets to allow you some of my skill at preparing seafood by making them cooking gauntlets.");
    ~chatnpc("<p,neutral>When you wear them you will burn lobsters, sharks and swordfish a lot less. It will be a permanent modification however, so make sure this is what you want.");
    def_int $op = ~p_choice2("Yes, please do that for me.", 1, "I'll see what your brothers have to offer first.", 2);
    if ($op = 1) {
        ~chatplayer("<p,neutral>Yes, please do that for me.");
        ~mesbox("Caleb takes the gauntlets from you, pours some herbs and seasonings on them, bakes them on his range for a short time, then hands them back to you.");
        %crest_spells_levers_gauntlets = setbit(%crest_spells_levers_gauntlets, ^crest_cooking_gauntlets_chosen);
        inv_del(inv, steel_gauntlets, 1);
        inv_add(inv, gauntlets_of_cooking, 1);
    }
    else {
        ~chatplayer("<p,neutral>I'll see what your brothers have to offer first.");
        ~chatnpc("<p,neutral>As you wish. Let me know if you change your mind.");
    }
} else {
    ~chatplayer("<p,neutral>I believe your father mentioned you would be able to improve some gauntlets for me...");
    ~chatnpc("<p,happy>Bring them to me, and I'll see what I can do.");
}
