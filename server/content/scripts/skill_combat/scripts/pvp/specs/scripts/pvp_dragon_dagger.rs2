[label,pvp_dragon_dagger_sa]
~set_sa_vars(oc_param(inv_getobj(worn, ^wearpos_rhand), sa_energy));
~set_pk_vars;
spotanim_pl(sp_attack_puncture_spotanim, 96, 0);
anim(puncture, 0);
sound_synth(puncture, 0, 0);
.anim(.%com_defendanim, 20);
.sound_synth(puncture, 0, 0);

~pvp_dragon_dagger_spec_hit;
p_delay(0); // p_delays before 2006 or so
~pvp_dragon_dagger_spec_hit;

[proc,pvp_dragon_dagger_spec_hit]
// If you're dead, 2nd hitsplat doesnt go through
// https://youtu.be/b4KCZ3ySHwc?t=131
if (stat(hitpoints) = 0) {
    return;
}
def_int $damage = 0;
def_int $maxhit = scale(115, 100, %com_maxhit); // 15% bonus damage
def_int $attack_roll = scale(115, 100, ~player_attack_roll_specific(%damagetype)); // 15% bonus accuracy
def_int $defence_roll = ~.player_defence_roll_specific(^slash_style); // puncture spec always rolls against their slash
if (randominc($attack_roll) > randominc($defence_roll)) {
    if (~.check_protect_prayer(^melee_style) = true) {
        $maxhit = scale(6, 10, $maxhit); // reduction of 40%
    }
    $damage = min(randominc($maxhit), .stat(hitpoints)); // tick eating existed! https://oldschool.runescape.wiki/w/Update:The_Wintertodt
    ~give_combat_experience_pvp(%damagestyle, $damage, ~pvp_xp_multiplier(~.player_combat_level));
    both_heropoints($damage);
}
.queue*(pvp_retaliate, 0)(uid);
~.pvp_damage(0, $damage);
def_int $poison_severity = oc_param(inv_getobj(worn, ^wearpos_rhand), poison_severity);
if ($damage > 0 & $poison_severity > 0 & random(4) = 0) { // 1/4 chance to poison
    .queue(poison_player, 0, $poison_severity);
}
