[proc,music_getvar](int $var)(int)
if ($var = 1) {
    return(%musicmulti_1);
} else if ($var = 2) {
    return(%musicmulti_2);
} else if ($var = 3) {
    return(%musicmulti_3);
} else if ($var = 4) {
    return(%musicmulti_4);
} else if ($var = 5) {
    return(%musicmulti_5);
} else if ($var = 6) {
    return(%musicmulti_6);
} else if ($var = 7) {
    return(%musicmulti_7);
}
return(null);

[proc,music_setvar](int $var, int $bit)
if ($var = 1) {
    %musicmulti_1 = setbit(%musicmulti_1, $bit);
} else if ($var = 2) {
    %musicmulti_2 = setbit(%musicmulti_2, $bit);
} else if ($var = 3) {
    %musicmulti_3 = setbit(%musicmulti_3, $bit);
} else if ($var = 4) {
    %musicmulti_4 = setbit(%musicmulti_4, $bit);
} else if ($var = 5) {
    %musicmulti_5 = setbit(%musicmulti_5, $bit);
} else if ($var = 6) {
    %musicmulti_6 = setbit(%musicmulti_6, $bit);
} else if ($var = 7) {
    %musicmulti_7 = setbit(%musicmulti_7, $bit);
}

[proc,music_playbybutton](component $playbutton)
db_find(music:playbutton, $playbutton);
def_dbrow $song = db_findnext;
if ($song = null) {
    return;
}

def_int $unlock;
def_int $bit;
$unlock, $bit = db_getfield($song, music:unlock, 0);

if ($unlock ! null) {
    if (testbit(~music_getvar($unlock), $bit) = ^false) {
        mes("You have not unlocked this piece of music yet!");
        return;
    }
}

%musicplay = 0; // switch to manual mode

def_string $name = db_getfield($song, music:name, 0);
if_settext(music:com_177, $name);
midi_song($name);

[label,music_playbyregion](coord $coord)
// first, search for the song linked to this mapsquare
def_int $map_x = calc(coordx($coord) / 64 * 64);
def_int $map_z = calc(coordz($coord) / 64 * 64);

db_find(musicregion:mapsquare, movecoord(0_0_0_0_0, $map_x, 0, $map_z));
def_dbrow $region = db_findnext;
if ($region = null) {
    return;
}

// now that we have the region, we can grab info on the song
def_dbrow $song = db_getfield($region, musicregion:musicdata, 0);
def_int $unlock;
def_int $bit;
$unlock, $bit = db_getfield($song, music:unlock, 0);

if ($unlock ! null) {
    if (testbit(~music_getvar($unlock), $bit) = ^false) {
        ~music_setvar($unlock, $bit);
    }
}

if (%musicplay ! 0) {
    def_string $name = db_getfield($song, music:name, 0);
    if_settext(music:com_177, $name);
    midi_song($name);
}
